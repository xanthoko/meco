reference comm_idl as idl
reference broker_dsl as bdsl

Entities:
    imports*=Import
    brokers+=Broker
    nodes*=Node
    bridges*=Bridge
    proxies*=RESTProxy
;

Broker:
    'Broker: ' broker=[bdsl.Broker] (default='(default)')?
;

Node:
    'Node' name=ID '{'
        (('inports:' inports*=InPort)?
        ('outports:' outports*=OutPort)?
        ('broker:' broker=[bdsl.Broker])?)#
    '}'
;

OutPort:
    Publisher | RPC_Client
;

Publisher:
    '- publisher:'
        ('topic:' topic=FQN
        'message:' object=[idl.PubSubMessage]
        ('frequency:' frequency=INT)?
        ('mock:' mock=BOOL)?)#

;

RPC_Client:
    '- rpc_client' name=FQN ':'
        ('message:' object=[idl.RPCMessage]
        ('frequency:' frequency=INT)?
        ('mock:' mock=BOOL)?)#
;

InPort:
    Subscriber | RPC_Service
;

Subscriber:
    '- subscriber:'
        'topic:' topic=FQN
;

RPC_Service:
    '- rpc_service' name=FQN ':'
        ('message:' object=[idl.RPCMessage])?
;

Bridge:
    TopicBridge | RPCBridge
;

TopicBridge:
    'TopicBridge' name=ID '{'
        brokerA=[bdsl.Broker] '(' fromTopic=FQN ')' '-'
        brokerB=[bdsl.Broker] '(' toTopic=FQN ')'
    '}'
;

RPCBridge:
    'RPCBridge' name=ID '{'
        brokerA=[bdsl.Broker] '(' nameA=ID ')' '-'
        brokerB=[bdsl.Broker] '(' nameB=ID ')'
    '}'
;


RESTProxy:
    'RESTProxy' name=ID '{'
        ('port:' port=RPC_Service
        'url:' url=STRING
        'method:' method=RequestMethods
        ('body:' body=[idl.Object])?
        ('query:' query=[idl.Object])?
        ('path:' path=[idl.Object])?
        ('header:' header=[idl.Object])?
        ('broker:' broker=[bdsl.Broker])?)#
    '}'
;


RequestMethods:
    'GET' | 'POST' | 'DELETE' | 'PATCH'
;

FQN: ID+['.'];
FQNI: ID+['.']('.*')?;
Import: 'import' importURI=FQNI ('as' name=ID)? ';';

// Comments
Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;
