reference comm_idl as idl
reference broker_dsl as bdsl

Entities:
    imports*=Import
    broker*=Broker
    nodes*=Node
;

Broker:
    'Broker: ' broker=[bdsl.Broker]
;

Node:
    InNode | OutNode | FuncNode
;

InNode:
    'InNode' name=ID '{'
        ('ports:' inports*=InPort
        'broker:' broker=[bdsl.Broker])#
    '}'
;

OutNode:
    'OutNode' name=ID '{'
        ('ports:' outports*=OutPort
        'broker:' broker=[bdsl.Broker])#
    '}'
;

OutPort:
    Publisher | RPC_Client
;

Publisher:
    '- publisher:'
        'topic:' topic=FQN
        'message:' object=[idl.PubSubMessage]
;

RPC_Client:
    '- rpc_client' name=ID ':'
        'message:' object=[idl.RPCMessage]
;

InPort:
    Subscriber | RPC_Service
;

Subscriber:
    '- subscriber:'
        'topic:' topic=FQN
;

RPC_Service:
    '- rpc_service' name=ID ':'
        'message:' object=[idl.RPCMessage]
;

FuncNode:
    Bridge
;

Bridge:
    'Bridge' name=ID '{'
        brokerA=[bdsl.Broker] '(' fromTopic=FQN ')' '-'
        brokerB=[bdsl.Broker] '(' toTopic=FQN ')'
    '}'
;

FQN: ID+['.'];
FQNI: ID+['.']('.*')?;
Import: 'import' importURI=FQNI ('as' name=ID)? ';';

// Comments
Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;
